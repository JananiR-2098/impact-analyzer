plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'checkstyle'
    id 'com.github.spotbugs' version '6.0.0'
    id 'pmd'
}

group = 'com.citi'
version = '0.0.1-SNAPSHOT'
description = 'impact-analyzer'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/milestone" } // Needed for Spring AI M6
}

ext {
    set('springAiVersion', "1.0.0-M6")
}

dependencies {
    // Reference the Spring AI BOM
    implementation platform("org.springframework.ai:spring-ai-bom:${springAiVersion}")

    // Spring Boot starter
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // Spring AI Vertex AI Gemini starter
    implementation 'org.springframework.ai:spring-ai-vertex-ai-gemini-spring-boot-starter'

    implementation 'org.springframework.ai:spring-ai-core'

    // Git operations
    implementation("org.eclipse.jgit:org.eclipse.jgit:7.4.0.202509020913-r")

    // Java parser for code analysis
    implementation 'com.github.javaparser:javaparser-core:3.25.4'

    // Langchain
    implementation("dev.langchain4j:langchain4j:1.8.0")
    implementation("dev.langchain4j:langchain4j-embeddings-all-minilm-l6-v2-q:1.8.0-beta15")
    implementation("dev.langchain4j:langchain4j-vertex-ai-gemini:1.8.0-beta15")
    implementation("io.grpc:grpc-netty-shaded:1.75.0")
    // Jackson is already included via Spring Boot BOM, no need to declare manually

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.12.2'

    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.12.0'
}

tasks.named('test') {
    useJUnitPlatform()
}

checkstyle {
    toolVersion = '10.13.0'
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
    ignoreFailures = false
    showViolations = true
    maxWarnings = Integer.MAX_VALUE
}

spotbugs {
    ignoreFailures = false
    effort = 'MAX'
    reportLevel = 'MEDIUM'
    toolVersion = '4.8.5'
}

spotbugsMain {
    reports {
        html.required = true
        xml.required = true
    }
}

spotbugsTest {
    reports {
        html.required = true
        xml.required = true
    }
}

pmd {
    toolVersion = '7.0.0'
    ignoreFailures = false
    ruleSetConfig = resources.text.fromFile("${rootDir}/config/pmd/ruleset.xml")
}

pmdMain {
    reports {
        html.required = true
        xml.required = true
    }
}

pmdTest {
    reports {
        html.required = true
        xml.required = true
    }
}

